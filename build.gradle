plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

allprojects {
    apply plugin: 'java'

    group = "com.lauriethefish.betterportals"

    if (project.file("commitversion.enable").exists()) {
        version = gradle.ext.versionName + '-' + "git rev-parse --verify --short HEAD".execute().text.trim().toLowerCase()
    } else {
       version = gradle.ext.versionName
    }

    // Optional, just for showing name
    afterEvaluate {
        println("Evaluating project ${project.group}:${archivesBaseName}:${version}")
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileJava.options.encoding = 'UTF-8'

    dependencies {
        implementation 'com.google.inject:guice:5.0.1'
        implementation 'com.google.inject.extensions:guice-assistedinject:5.0.1'
        implementation 'org.jetbrains:annotations:23.0.0'
        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        testImplementation 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    }

    tasks {
        compileJava {
            options.release.set(17)
        }
    }
}

subprojects {
    archivesBaseName = "${rootProject.name}-${project.name}"
}

dependencies {
    implementation project(':shared')
    implementation project(path: ':bukkit', configuration: 'reobf')
    implementation project(':bungee')
    implementation project(':api')
}

def getImplementationConfigurations(parentProject) {
    parentProject.configurations.implementation.canBeResolved = true
    return parentProject.configurations.implementation
}

tasks.build.dependsOn tasks.shadowJar
shadowJar {
    configurations = [getImplementationConfigurations(project), getImplementationConfigurations(project(':bukkit'))]

    relocate 'org.bstats', 'com.lauriethefish.bstats'
    relocate 'com.google', 'com.lauriethefish.google'
}
